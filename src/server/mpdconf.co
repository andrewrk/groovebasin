const
  path = require('path')
  osenv = require('osenv')

  ALL_PERMISSIONS =
    read: true
    add: true
    control: true
    admin: true

  DEFAULT_PERMISSIONS =
    read: true
    add: true
    control: true

function permsToList (perms)
  Object.keys(perms).join(',')

module.exports = class MpdConf

  (@state) ~>
    if @state!? then @setDefaultState()

  setDefaultState: !->
    @state = {}
    @state.admin_password = Math.floor(Math.random() * 99999999999)
    @state.permissions = "#{@state.admin_password}": ALL_PERMISSIONS

    @state.audio_httpd =
      format: \ogg
      quality: 6
      port: 16243
    @state.audio_pulse = null
    @state.audio_alsa = null
    @state.audio_oss = null

    @state.run_dir = null
    @state.music_directory = path.join(osenv.home(), 'music')
    @state.port = 16244
    @state.default_permissions = DEFAULT_PERMISSIONS

  accountHasPerm: (passwd, perm) -> !!@state.permissions[passwd][perm]

  playlistDirectory: -> path.join(@state.run_dir, "playlists")

  setRunDir: !-> @state.run_dir = path.resolve(it)

  toMpdConf: ->
    accounts = ""
    for password, perms in @state.permissions
      accounts += "password \"#{password}@#{permsToList(perms)}\"\n"

    audio_outputs = ""
    if @state.audio_httpd?
      if @state.audio_httpd.format is \ogg
        quality = "quality \"#{@state.audio_httpd.quality}\""
        bitrate = ""
        encoder_value = "vorbis"
      else if @state.audio_httpd.format is \mp3
        quality = ""
        bitrate = "bitrate \"#{@state.audio_httpd.bitrate}\""
        encoder_value = "lame"
      audio_outputs += """
        audio_output {
          type            "httpd"
          name            "Groove Basin"
          encoder         "#encoder_value"
          port            "#{@state.audio_httpd.port}"
          bind_to_address "0.0.0.0"
          #quality
          #bitrate
          format          "44100:16:2"
          max_clients     "0"
        }
      """
    if @state.audio_pulse?
      audio_outputs += """
        audio_output {
          type  "pulse"
          name  "Groove Basin"
        }
      """
    if @state.audio_alsa?
      audio_outputs += """
        audio_output {
          type        "alsa"
          name        "Groove Basin"
        }
      """
    if @state.audio_oss?
      audio_outputs += """
        audio_output {
          type        "oss"
          name        "Groove Basin"
        }
      """
    # mpd requires a null output if none other
    audio_outputs ||= """
      audio_output {
        type "null"
        name "Groove Basin"
      }
    """

    """
    music_directory         "#{@state.music_directory}"
    playlist_directory      "#{@playlistDirectory()}"
    db_file                 "#{path.join(@state.run_dir, "mpd.music.db")}"
    log_file                "#{path.join(@state.run_dir, "mpd.log")}"
    pid_file                "#{path.join(@state.run_dir, "mpd.pid")}"
    state_file              "#{path.join(@state.run_dir, "mpd.state")}"
    sticker_file            "#{path.join(@state.run_dir, "mpd.sticker.db")}"
    bind_to_address         "localhost"
    bind_to_address         "#{path.join(@state.run_dir, "mpd.socket")}"
    port                    "#{@state.port}"
    gapless_mp3_playback    "yes"
    auto_update             "yes"
    default_permissions     "#{permsToList(@state.default_permissions)}"
    replaygain              "album"
    volume_normalization    "yes"
    max_command_list_size   "16384"
    max_connections         "10"
    max_output_buffer_size  "16384"
    id3v1_encoding          "UTF-8"
    #accounts
    #audio_outputs
    """

